{
  description = "my neovim config :3";

  inputs = {
    nixpkgs.url = "https://channels.nixos.org/nixos-unstable/nixexprs.tar.xz";

    gift-wrap = {
      url = "github:tgirlcloud/gift-wrap";
      inputs.nixpkgs.follows = "nixpkgs";
    };

    neovim-nightly = {
      url = "github:nix-community/neovim-nightly-overlay";
      inputs.nixpkgs.follows = "nixpkgs";
    };
  };

  outputs =
    {
      self,
      nixpkgs,
      ...
    }@inputs:
    let
      inherit (nixpkgs) lib;
      systems = lib.systems.flakeExposed;

      forAllSystems =
        f:
        lib.genAttrs systems (
          system:
          f (
            import nixpkgs {
              inherit system;
              overlays = [ inputs.neovim-nightly.overlays.default ];
            }
          )
        );

      mkPackages =
        default: pkgs:
        let
          generatedPackages = import ./nix/default.nix { inherit pkgs inputs; };
          defaultPackage = lib.optionalAttrs default { default = generatedPackages.anvim; };
        in
        generatedPackages // defaultPackage;
    in
    {
      packages = forAllSystems (mkPackages true);

      homeManagerModules.default = import ./nix/hm-module.nix inputs;

      devShells = forAllSystems (pkgs: {
        default = pkgs.callPackage ./nix/shell.nix { inherit pkgs; };
      });

      apps = forAllSystems (pkgs: {
        update = {
          type = "app";
          program = lib.getExe (
            pkgs.writeShellApplication {
              name = "update";
              runtimeInputs = [ pkgs.npins ];

              text = ''
                (cd pkgs/anvim-plugins && npins update)
              '';
            }
          );
        };
      });

      formatter = forAllSystems (pkgs: pkgs.callPackage ./nix/formatter.nix { inherit pkgs; });
    };
}
